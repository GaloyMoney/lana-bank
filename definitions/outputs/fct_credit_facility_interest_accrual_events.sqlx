config {
    type: "table",
    description: "Each row represents a unique event/fact of the interest accrual [interest_accrual_concluded] of the credit facility.",
    assertions: {
        uniqueKeys: [["event_key", "idx"]],
        nonNull: [
            "recorded_at_date_key",
            "recorded_at",
            "event_type_key",
            "accrued_at_date_key",
            "accrued_at",
            "amount",
        ],
    },
}

WITH interest_accrual_concluded AS (

    SELECT
          event_key
        , CAST(FORMAT_DATE('%Y%m%d', recorded_at) as INT64) AS recorded_at_date_key
        , recorded_at
        , event_type_key
        , CAST(FORMAT_DATE('%Y%m%d', PARSE_TIMESTAMP('%Y-%m-%dT%H:%M:%E*SZ', JSON_VALUE(event, "$.accrued_at"), "UTC")) as INT64) AS accrued_at_date_key
        , PARSE_TIMESTAMP('%Y-%m-%dT%H:%M:%E*SZ', JSON_VALUE(event, "$.accrued_at"), "UTC") AS accrued_at
        , CAST(JSON_VALUE(event, "$.amount") AS NUMERIC) AS amount
        , CAST(JSON_VALUE(event, "$.idx") AS INTEGER) AS idx
    FROM ${ref({name: "credit_facility_events", schema: envs.currentImportSchema})} AS cfe
    LEFT JOIN ${ref("dim_credit_facility_events_id")} AS t0 ON t0.event_id = cfe.id
    LEFT JOIN ${ref("dim_credit_facility_events_type")} AS t1 ON t1.event_type = cfe.event_type
    WHERE cfe.event_type = "interest_accrual_concluded"
    AND JSON_VALUE(event, "$.tx_id") IS NOT NULL

)


SELECT
      *
FROM interest_accrual_concluded
