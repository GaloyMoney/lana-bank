config {
    type: "table",
    description: "Each row represents a unique 'combo-event'/fact? of disbursements [disbursal_initiated -> disbursal_concluded] of the credit facility.",
    assertions: {
        uniqueKeys: [["event_key", "idx"]],
        nonNull: [
            "recorded_at_date_key",
            "recorded_at",
            "event_type_key",
            "disbursal_concluded_event_recorded_at_date_key",
            "amount",
        ],
    },
}

WITH disbursal_initiated AS (

    SELECT
          event_key
        , CAST(FORMAT_DATE('%Y%m%d', recorded_at) as INT64) AS recorded_at_date_key
        , recorded_at
        , event_type_key
        , CAST(JSON_VALUE(event, "$.amount") AS NUMERIC) AS amount
        , CAST(JSON_VALUE(event, "$.idx") AS INTEGER) AS idx
    FROM ${ref({name: "credit_facility_events", schema: envs.currentImportSchema})} AS cfe
    LEFT JOIN ${ref("dim_credit_facility_events_id")} AS t0 ON t0.event_id = cfe.id
    LEFT JOIN ${ref("dim_credit_facility_events_type")} AS t1 ON t1.event_type = cfe.event_type
    WHERE cfe.event_type = "disbursal_initiated"

), disbursal_concluded AS (

    SELECT
          event_key
        , CAST(FORMAT_DATE('%Y%m%d', recorded_at) as INT64) AS recorded_at_date_key
        , recorded_at
        , CAST(FORMAT_DATE('%Y%m%d', PARSE_TIMESTAMP('%Y-%m-%dT%H:%M:%E*SZ', JSON_VALUE(event, "$.recorded_at"), "UTC")) as INT64) AS event_recorded_at_date_key
        , PARSE_TIMESTAMP('%Y-%m-%dT%H:%M:%E*SZ', JSON_VALUE(event, "$.recorded_at"), "UTC") AS event_recorded_at
        , CAST(JSON_VALUE(event, "$.idx") AS INTEGER) AS idx
    FROM ${ref({name: "credit_facility_events", schema: envs.currentImportSchema})} cfe
    LEFT JOIN ${ref("dim_credit_facility_events_id")} AS t0 ON t0.event_id = cfe.id
    WHERE cfe.event_type = "disbursal_concluded"
    AND JSON_VALUE(event, "$.tx_id") IS NOT NULL

)


SELECT
      di.* EXCEPT (amount)

    , COALESCE(dc.event_recorded_at_date_key, 19000101) AS disbursal_concluded_event_recorded_at_date_key
    , dc.event_recorded_at AS disbursal_concluded_event_recorded_at

    , di.amount
FROM disbursal_initiated AS di
LEFT JOIN disbursal_concluded dc ON dc.event_key = di.event_key AND di.idx = dc.idx
