config {
	type: "table",
}

WITH line_of_credit_terms AS (

	SELECT *
	FROM ${ref("stg_approved_credit_facilities_terms")}

), interest_payment_data AS (

	SELECT
		  *
		, SAFE_DIVIDE(
			loc_annual_interest_rate / 100.0,
			CASE
				WHEN ENDS_WITH(loc_day_count_convention, '/360') THEN 360.0
				WHEN ENDS_WITH(loc_day_count_convention, '/365') THEN 365.0
				ELSE TIMESTAMP_DIFF(TIMESTAMP(LAST_DAY(DATE(loc_recorded_at), YEAR)), DATE_TRUNC(loc_recorded_at, YEAR), DAY)
			END
		  ) AS loc_daily_interest_rate
		, CASE
			WHEN ENDS_WITH(loc_day_count_convention, '/360') THEN 360.0
			WHEN ENDS_WITH(loc_day_count_convention, '/365') THEN 365.0
			ELSE TIMESTAMP_DIFF(TIMESTAMP(LAST_DAY(DATE(loc_recorded_at), YEAR)), DATE_TRUNC(loc_recorded_at, YEAR), DAY)
		  END AS days_per_year
		, initiated_disbursement_at AS disbursement_start_date
		, TIMESTAMP(DATE_TRUNC(DATE(loc_recorded_at), month)) AS period_start_date
		, loc_end_date AS period_end_date
	FROM ${ref("stg_approved_credit_facilities_disbursement")}

), projected_principal_time AS (

	SELECT
		  *
		, TIMESTAMP_DIFF(period_end_date, period_start_date, DAY) AS days_in_the_period
		, SAFE_DIVIDE(TIMESTAMP_DIFF(period_end_date, period_start_date, DAY), days_per_year) AS years_in_the_period
		, TIMESTAMP_DIFF(period_end_date, now_ts, DAY) AS days_from_now
		, SAFE_DIVIDE(TIMESTAMP_DIFF(period_end_date, now_ts, DAY), days_per_year) AS years_from_now
	FROM interest_payment_data

), projected_principal_payment AS (

	SELECT
		  *
		, loc_end_date AS recorded_at
		, 'projected_principal_payment' AS event_type
		, SAFE_NEGATE(disbursement_amount_in_cents) AS amount_in_cents
	FROM projected_principal_time

)

SELECT * FROM projected_principal_payment
