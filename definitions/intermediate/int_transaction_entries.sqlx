config {
	type: "view",
	assertions: {
		uniqueKey: ["transaction_entry_id"],
		nonNull: ["transaction_entry_id", "recorded_at", "account_id", "currency", "direction", "journal_id", "transaction_id", "layer", "sequence", "units"],
		rowConditions: [
			"direction IN ('debit', 'credit')",
			"layer IN ('Settled', 'Pending')",
			"units >= 0",
		],
	},
}

SELECT id AS transaction_entry_id,
	ROW_NUMBER() OVER() AS transaction_entry_key,
	version AS transaction_entry_version,
	recorded_at,
	JSON_VALUE(values, "$.account_id") AS account_id,
	JSON_VALUE(values, "$.currency") AS currency,
	JSON_VALUE(values, "$.direction") AS direction,
	JSON_VALUE(values, "$.entry_type") AS entry_type,
	JSON_VALUE(values, "$.journal_id") AS journal_id,
	JSON_VALUE(values, "$.transaction_id") AS transaction_id,
	JSON_VALUE(values, "$.layer") AS layer,
	CAST(JSON_VALUE(values, "$.sequence") AS INT64) AS sequence,
	CAST(JSON_VALUE(values, "$.units") AS NUMERIC) AS units,

FROM ${ref({
	name: "entries",
	schema: envs.currentImportSchema
})}
