config {
	type: "table",
}

SELECT customer_id,
	JSON_VALUE(content, "$.id") AS applicant_id,
	TIMESTAMP(JSON_VALUE(content, "$.createdAt")) AS created_at,
	JSON_VALUE(content, "$.info.firstName") AS first_name,
	JSON_VALUE(content, "$.info.lastName") AS last_name,
	DATE(JSON_VALUE(content, "$.info.dob")) AS date_of_birth,
	JSON_VALUE(content, "$.info.gender") AS gender,
	JSON_VALUE(content, "$.info.country") AS iso_alpha_3_code,
	JSON_VALUE(content, "$.info.nationality") AS nationality_iso_alpha_3_code,
	ARRAY(
		SELECT AS STRUCT
			JSON_VALUE(doc, "$.country") AS iso_alpha_3_code,
			JSON_VALUE(doc, "$.idDocType") AS type,
			JSON_VALUE(doc, "$.number") AS number,
		FROM UNNEST(JSON_QUERY_ARRAY(content, "$.info.idDocs")) AS doc
	) AS id_documents,

	ARRAY(
		SELECT AS STRUCT
			JSON_VALUE(questions, "$.sections.personalInformation.items.occupation.value") AS occupation_code,
			JSON_VALUE(questions, "$.sections.personalInformation.items.economicActivity.value") AS economic_activity_code,
			JSON_VALUE(questions, "$.sections.personalInformation.items.countryOfResidence.value") AS country_of_residence_iso_alpha_3_code,
			JSON_VALUE(questions, "$.sections.personalInformation.items.taxIdentificationNum.value") AS tax_id_number,
		FROM UNNEST(JSON_QUERY_ARRAY(content, "$.questionnaires")) AS questions
	) AS questionnaires,

FROM ${ref({
	name: "sumsub_applicants",
	schema: envs.currentImportSchema
})}

WHERE content_type = "SensitiveInfo"
