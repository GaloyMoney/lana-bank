config {
	type: "view",
}

WITH line_of_credit_terms AS (

	SELECT *
	FROM ${ref("int_approved_credit_facilities_terms")}

), interest_payment_data AS (

	SELECT
		  *
		, concluded_disbursement_at AS disbursement_start_date
		, CASE WHEN loc_interest_accrual_interval = 'end_of_month' THEN
			GENERATE_DATE_ARRAY(DATE(concluded_disbursement_at), LAST_DAY(DATE(loc_end_date)), INTERVAL 1 MONTH)
		  END AS interest_schedule_months
	FROM ${ref("int_approved_credit_facilities_disbursement")}

), projected_interest_payment_data AS (

	SELECT
		  * except(interest_schedule_months)
		, CASE WHEN TIMESTAMP(DATE_TRUNC(projected_month, MONTH)) < disbursement_start_date THEN
			disbursement_start_date
		  ELSE
			TIMESTAMP(DATE_TRUNC(projected_month, MONTH))
		  END AS period_start_date
		, CASE WHEN LAST_DAY(projected_month) > DATE(loc_end_date) THEN
			loc_end_date
		  ELSE
			TIMESTAMP(LAST_DAY(projected_month))
		  END AS period_end_date
	FROM interest_payment_data,
	     UNNEST(interest_schedule_months) AS projected_month

), projected_interest_time AS (

	SELECT
		  *
		, TIMESTAMP_DIFF(DATE(period_end_date), DATE(period_start_date), DAY) + 1 AS days_in_the_period
		, SAFE_DIVIDE(TIMESTAMP_DIFF(DATE(period_end_date), DATE(period_start_date), DAY) + 1, days_per_year) AS years_in_the_period
		, CAST(TIMESTAMP_DIFF(DATE(period_end_date), DATE(now_ts), DAY) + 1 AS FLOAT64) AS days_from_now
		, SAFE_DIVIDE(TIMESTAMP_DIFF(DATE(period_end_date), DATE(now_ts), DAY) + 1, 365.0) AS years_from_now
	FROM projected_interest_payment_data

), projected_interest_payment AS (

	SELECT
		  *
		, period_end_date AS recorded_at
		, 'projected_interest_payment' AS event_type
		, SAFE_MULTIPLY(SAFE_NEGATE(disbursement_amount_in_cents), SAFE_MULTIPLY(loc_daily_interest_rate, days_in_the_period)) AS amount_in_cents
	FROM projected_interest_time

)

SELECT * FROM projected_interest_payment
