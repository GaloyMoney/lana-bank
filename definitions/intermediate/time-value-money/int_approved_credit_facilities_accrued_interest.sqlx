config {
	type: "view",
}

WITH interest_accrual AS (

	SELECT
		  terms.*
		, id AS event_id
		, recorded_at AS interest_accrual_recorded_at
		, event_type AS interest_accrual_event_type
		, JSON_VALUE(event, "$.started_at") AS interest_accrual_started_at
		, JSON_VALUE(event, "$.facility_expires_at") AS facility_expires_at
		, JSON_VALUE(event, "$.terms.accrual_interval.type") AS terms_accrual_interval_type
		, CAST(JSON_VALUE(event, "$.terms.annual_rate") AS NUMERIC) AS terms_annual_rate
		, JSON_VALUE(event, "$.terms.duration.type") AS terms_duration_type
		, CAST(JSON_VALUE(event, "$.terms.duration.value") AS INTEGER) AS terms_duration_value
		, JSON_VALUE(event, "$.terms.incurrence_interval.type") AS terms_incurrence_interval_type
		, CAST(JSON_VALUE(event, "$.terms.initial_cvl") AS NUMERIC) AS terms_initial_cvl
		, CAST(JSON_VALUE(event, "$.terms.liquidation_cvl") AS NUMERIC) AS terms_liquidation_cvl
		, CAST(JSON_VALUE(event, "$.terms.margin_call_cvl") AS NUMERIC) AS terms_margin_call_cvl
	FROM ${ref({name: "interest_accrual_events", schema: envs.currentImportSchema})}
	LEFT JOIN ${ref("int_approved_credit_facilities_terms")} terms ON terms.loc_id = JSON_VALUE(event, "$.facility_id")
	WHERE event_type = "initialized"

)

SELECT
	  ia.*

	, CAST(JSON_VALUE(event, "$.accrued_at") AS TIMESTAMP) AS interest_accrued_at
	, ii.event_type
	, CAST(JSON_VALUE(event, "$.total") AS NUMERIC) AS amount_in_cents
FROM ${ref({name: "interest_accrual_events", schema: envs.currentImportSchema})} ii
LEFT JOIN interest_accrual ia ON ia.event_id = ii.id
WHERE event_type = "interest_accrued"
