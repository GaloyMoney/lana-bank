config {
	type: "view",
}

WITH line_of_credit_terms AS (

	SELECT
		  JSON_VALUE(event, "$.customer_id") AS customer_id
		, id AS loc_id
		, recorded_at AS loc_recorded_at
		, event_type AS loc_event_type

		, CAST(JSON_VALUE(event, "$.facility") AS NUMERIC) AS loc_borrow_limit_in_cents
		, recorded_at AS loc_initialized_timestamp
		, SAFE_DIVIDE(CAST(JSON_VALUE(event, "$.terms.annual_rate") AS NUMERIC), 100.0) AS loc_annual_interest_rate

		, 'actual/360' AS loc_day_count_convention	-- TODO get from proper source
		, 5.53 / 100.0 AS bench_mark_interest_rate	-- TODO get from proper source
		, CURRENT_TIMESTAMP() AS now_ts

		, CAST(JSON_VALUE(event, "$.terms.duration.value") AS INTEGER) AS loc_period_count
		, JSON_VALUE(event, "$.terms.duration.type") AS loc_period

		, CASE WHEN JSON_VALUE(event, "$.terms.duration.type") = 'months' THEN
			TIMESTAMP(TIMESTAMP_ADD(DATE(recorded_at), INTERVAL CAST(JSON_VALUE(event, "$.terms.duration.value") AS INT64) MONTH))
		--   ELSE
		-- 	TIMESTAMP_ADD(DATE(recorded_at), INTERVAL 1 YEAR)
		  END AS loc_end_date

		, JSON_VALUE(event, "$.terms.accrual_interval.type") AS loc_interest_accrual_interval
		, JSON_VALUE(event, "$.terms.incurrence_interval.type") AS loc_interest_incurrence_interval

		, CAST(JSON_VALUE(event, "$.terms.initial_cvl") AS NUMERIC) AS loc_initial_cvl
		, CAST(JSON_VALUE(event, "$.terms.liquidation_cvl") AS NUMERIC) AS loc_liquidation_cvl
		, CAST(JSON_VALUE(event, "$.terms.margin_call_cvl") AS NUMERIC) AS loc_margin_call_cvl
	FROM ${ref({name: "credit_facility_events", schema: envs.currentImportSchema})}
	WHERE event_type = "initialized"

), approved_line_of_credit AS (

	SELECT
		  terms.*
		, SAFE_DIVIDE(bench_mark_interest_rate, 365.0) AS bench_mark_daily_interest_rate
		, SAFE_DIVIDE(
			loc_annual_interest_rate,
			CASE
				WHEN ENDS_WITH(loc_day_count_convention, '/360') THEN 360.0
				WHEN ENDS_WITH(loc_day_count_convention, '/365') THEN 365.0
				ELSE TIMESTAMP_DIFF(TIMESTAMP(LAST_DAY(DATE(loc_recorded_at), YEAR)), DATE_TRUNC(loc_recorded_at, YEAR), DAY)
			END
		  ) AS loc_daily_interest_rate
		, CASE
			WHEN ENDS_WITH(loc_day_count_convention, '/360') THEN 360.0
			WHEN ENDS_WITH(loc_day_count_convention, '/365') THEN 365.0
			ELSE TIMESTAMP_DIFF(TIMESTAMP(LAST_DAY(DATE(loc_recorded_at), YEAR)), DATE_TRUNC(loc_recorded_at, YEAR), DAY)
		  END AS days_per_year
		, SAFE_DIVIDE(bench_mark_interest_rate, loc_annual_interest_rate) AS breakeven_disbursement_percent
		, SAFE_MULTIPLY(
			loc_borrow_limit_in_cents,
			SAFE_DIVIDE(bench_mark_interest_rate, loc_annual_interest_rate)
		) AS breakeven_disbursement_amount_in_cents
		, SAFE_SUBTRACT(SAFE_DIVIDE(loc_margin_call_cvl, loc_initial_cvl), 1.0) AS margin_call_price_shift
		, SAFE_SUBTRACT(SAFE_DIVIDE(loc_liquidation_cvl, loc_initial_cvl), 1.0) AS liquidation_price_shift
		, cfe.recorded_at AS loc_approved_at
	FROM ${ref({name: "credit_facility_events", schema: envs.currentImportSchema})} cfe
	LEFT JOIN line_of_credit_terms terms ON terms.loc_id = cfe.id
	WHERE event_type = "approval_process_concluded"
	AND JSON_VALUE(event, "$.approved") = 'true'

)

SELECT * FROM approved_line_of_credit
