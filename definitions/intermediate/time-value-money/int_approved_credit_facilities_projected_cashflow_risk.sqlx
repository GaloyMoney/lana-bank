config {
	type: "view",
}

WITH projected_interest_payment AS (

	SELECT
		  customer_id
		, loc_id
		, loc_recorded_at
		, loc_approved_at
		, loc_end_date
		, bench_mark_interest_rate
		, bench_mark_daily_interest_rate
		, loc_annual_interest_rate
		, loc_daily_interest_rate
		, now_ts
		, days_per_year
		, disbursement_amount_in_cents
		, days_in_the_period
		, days_from_now
		, years_from_now
		, amount_in_cents
	FROM ${ref("int_approved_credit_facilities_projected_interest_payment")}
	WHERE recorded_at > now_ts

), projected_principal_payment AS (

	SELECT
		  customer_id
		, loc_id
		, loc_recorded_at
		, loc_approved_at
		, loc_end_date
		, bench_mark_interest_rate
		, bench_mark_daily_interest_rate
		, loc_annual_interest_rate
		, loc_daily_interest_rate
		, now_ts
		, days_per_year
		, disbursement_amount_in_cents
		, days_in_the_period
		, days_from_now
		, years_from_now
		, amount_in_cents
	FROM ${ref("int_approved_credit_facilities_projected_principal_payment")}
	WHERE recorded_at > now_ts

), unioned AS (

	SELECT * FROM projected_interest_payment UNION ALL
	SELECT * FROM projected_principal_payment

), grouped AS (

	SELECT
		  customer_id
		, loc_id
		, loc_recorded_at
		, loc_approved_at
		, loc_end_date
		, bench_mark_interest_rate
		, bench_mark_daily_interest_rate
		, loc_annual_interest_rate
		, loc_daily_interest_rate
		, now_ts
		, days_per_year
		, disbursement_amount_in_cents
		, MAX(days_in_the_period) AS days_in_the_period
		, days_from_now
		, years_from_now
		, SUM(amount_in_cents) AS amount_in_cents
	FROM unioned
	GROUP BY
		  customer_id
		, loc_id
		, loc_recorded_at
		, loc_approved_at
		, loc_end_date
		, bench_mark_interest_rate
		, bench_mark_daily_interest_rate
		, loc_annual_interest_rate
		, loc_daily_interest_rate
		, now_ts
		, days_per_year
		, disbursement_amount_in_cents
		, days_from_now
		, years_from_now
	ORDER BY days_from_now

), arrayed AS (

	SELECT
		  customer_id
		, loc_id
		, loc_recorded_at
		, loc_approved_at
		, loc_end_date
		, bench_mark_interest_rate
		, bench_mark_daily_interest_rate
		, loc_annual_interest_rate
		, loc_daily_interest_rate
		, now_ts
		, days_per_year
		, disbursement_amount_in_cents
		, ARRAY_AGG(days_in_the_period) AS days_in_the_period
		, ARRAY_AGG(days_from_now) AS days_from_now
		, ARRAY_AGG(years_from_now) AS years_from_now
		, ARRAY_AGG(amount_in_cents) AS cash_flows
	FROM grouped
	GROUP BY
		  customer_id
		, loc_id
		, loc_recorded_at
		, loc_approved_at
		, loc_end_date
		, bench_mark_interest_rate
		, bench_mark_daily_interest_rate
		, loc_annual_interest_rate
		, loc_daily_interest_rate
		, now_ts
		, days_per_year
		, disbursement_amount_in_cents

)

SELECT
	  *
	, ${ref("udf_loan_pv")}(bench_mark_daily_interest_rate, days_from_now, cash_flows) AS pv
	, ${ref("udf_loan_ytm")}(bench_mark_daily_interest_rate, days_from_now, cash_flows) AS ytm
	, ${ref("udf_loan_ytm_from_price")}(ABS(disbursement_amount_in_cents), days_from_now, cash_flows) AS ytm_from_price
	, ${ref("udf_loan_duration")}(bench_mark_daily_interest_rate, days_from_now, cash_flows) AS duration
	, ${ref("udf_loan_mac_duration")}(bench_mark_daily_interest_rate, days_from_now, cash_flows) AS mac_duration
	, TIMESTAMP_ADD(DATE(now_ts), INTERVAL CAST(${ref("udf_loan_mac_duration")}(bench_mark_daily_interest_rate, days_from_now, cash_flows) AS INT64) DAY) AS mac_duration_date
	, ${ref("udf_loan_mod_duration")}(bench_mark_daily_interest_rate, days_from_now, cash_flows) AS mod_duration
	, ${ref("udf_loan_convexity")}(bench_mark_daily_interest_rate, days_from_now, cash_flows) AS convexity
	, ${ref("udf_loan_pv_delta_on_interest_rate_delta_with_convex")}(bench_mark_daily_interest_rate, days_from_now, cash_flows, 0.0001 / days_per_year) AS dv01
	, ${ref("udf_loan_pv")}(bench_mark_daily_interest_rate + (0.0001 / days_per_year), days_from_now, cash_flows) AS pv_at_dv01
FROM arrayed
