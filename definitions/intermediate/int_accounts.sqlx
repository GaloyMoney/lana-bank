config {
	type: "table",
	assertions: {
		uniqueKey: ["account_id"],
	},
}

WITH all_accounts AS (

	SELECT
		id AS account_id,
		JSON_VALUE(ANY_VALUE(values HAVING MAX recorded_at), "$.name") AS account_name,
		JSON_VALUE(ANY_VALUE(values HAVING MAX recorded_at), "$.normal_balance_type") AS normal_balance_type,
		JSON_VALUE(ANY_VALUE(values HAVING MAX recorded_at), "$.code") AS account_code,
		LAX_BOOL(PARSE_JSON(JSON_VALUE(ANY_VALUE(values HAVING MAX recorded_at), "$.config.is_account_set"))) AS is_account_set,

	FROM ${ref({
		name: "accounts",
		schema: envs.currentImportSchema
	})}

	GROUP BY id

), credit_facilities AS (

	SELECT DISTINCT credit_facility_key,
		collateral_account_id,
		disbursed_receivable_account_id,
		facility_account_id,
		fee_income_account_id,
		interest_account_id,
		interest_receivable_account_id,

	FROM ${ref("int_approved_credit_facilities")}(
		TIMESTAMP_TRUNC(CURRENT_TIMESTAMP(), DAY)
	)

), credit_facility_accounts AS (

	SELECT DISTINCT credit_facility_key,
		collateral_account_id AS account_id,
		"collateral_account" AS account_type,
	FROM credit_facilities

	UNION DISTINCT

	SELECT DISTINCT credit_facility_key,
		disbursed_receivable_account_id AS account_id,
		"disbursed_receivable_account" AS account_type,
	FROM credit_facilities

	UNION DISTINCT

	SELECT DISTINCT credit_facility_key,
		facility_account_id AS account_id,
		"facility_account" AS account_type,
	FROM credit_facilities

	UNION DISTINCT

	SELECT DISTINCT credit_facility_key,
		fee_income_account_id AS account_id,
		"fee_income_account" AS account_type,
	FROM credit_facilities

	UNION DISTINCT

	SELECT DISTINCT credit_facility_key,
		interest_account_id AS account_id,
		"interest_account" AS account_type,
	FROM credit_facilities

	UNION DISTINCT

	SELECT DISTINCT credit_facility_key,
		interest_receivable_account_id AS account_id,
		"interest_receivable_account" AS account_type,
	FROM credit_facilities

)

SELECT account_id, account_name, normal_balance_type, account_code, is_account_set,
	credit_facility_key, account_type,
	ROW_NUMBER() OVER() AS account_key,

FROM all_accounts
LEFT JOIN credit_facility_accounts USING (account_id)
