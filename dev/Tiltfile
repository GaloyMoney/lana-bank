# Workstream support: read port offset and project name from environment
port_offset = int(os.getenv("WORKSTREAM_PORT_OFFSET", "0"))
project_name = os.getenv("COMPOSE_PROJECT_NAME", "lana-bank")

# Build compose files list
compose_files = ["../docker-compose.yml", "../docker-compose.docker.yml"]

# Include workstream port overrides if present
if os.path.exists("../docker-compose.workstream.yml"):
    compose_files.append("../docker-compose.workstream.yml")

if os.getenv("DAGSTER", "false") == "true":
    compose_files.append("../docker-compose.dagster.yml")

docker_compose(compose_files, project_name = project_name)

docker_groups = {
    "auth": ["keycloak", "keycloak-pg", "oathkeeper"],
    "otel": ["otel-agent"],
    "core": ["core-pg"],
}

for service, deps in docker_groups.items():
    for dep in deps:
        dc_resource(dep, labels = [service])

if os.getenv("DAGSTER", "false") == "true":
    dagster_services = [
        {
            "service_name": "dagster_webserver",
            "link": link("http://localhost:%d" % (3000 + port_offset), "dagster-webserver")
        },
        {
            "service_name": "dagster_daemon"
        },
        {
            "service_name": "dagster_postgres"
        },
        {
            "service_name": "dagster-code-location-lana-dw"
        },
    ]
    for dagster_service in dagster_services:
        if dagster_service.get("link", None):
            dc_resource(
                dagster_service["service_name"],
                labels = ["dagster"],
                links = [
                    dagster_service.get("link", None)
                ]
            )
        else:
            dc_resource(
                dagster_service["service_name"],
                labels = ["dagster"]
            )


serve_env_core = {
    "PG_CON": "postgres://user:password@localhost:%d/pg?sslmode=disable" % (5433 + port_offset),
    "OTEL_EXPORTER_OTLP_ENDPOINT": "http://localhost:%d" % (4317 + port_offset),
}
local_resource(
    "core",
    labels = ["core"],
    serve_cmd = "cd .. && make setup-db run-server",
    serve_env = serve_env_core,
    readiness_probe = probe(
        initial_delay_secs = 3,
        period_secs = 1,
        http_get = http_get_action(
            path = "/graphql",
            port = 5253,
        ),
    ),
    allow_parallel = True,
    resource_deps = [
        "core-pg",
        "keycloak"
    ],
    links = [
        link("http://admin.localhost:%d/graphql" % (4455 + port_offset), "playground"),
    ]
)

local_resource(
    "admin-panel",
    labels = ["apps"],
    serve_env = {
        "NEXT_PUBLIC_CORE_ADMIN_URL": "/graphql"
    },
    serve_cmd = "cd .. && make start-admin",
    readiness_probe = probe(
        initial_delay_secs = 3,
        period_secs = 1,
        http_get = http_get_action(
            path = "/",
            port = 3001,
        ),
    ),
    allow_parallel = True,
    links = [
        link("http://admin.localhost:%d" % (4455 + port_offset), "admin-panel"),
    ]
)

local_resource(
    "customer-portal",
    labels = ["apps"],
    serve_env = {
        "NEXT_PUBLIC_CORE_APP_URL": "/graphql"
    },
    serve_cmd = "cd .. && make start-customer-portal",
    readiness_probe = probe(
        initial_delay_secs = 3,
        period_secs = 1,
        http_get = http_get_action(
            path = "/",
            port = 3002,
        ),
    ),
    allow_parallel = True,
    links = [
        link("http://app.localhost:%d" % (4455 + port_offset), "customer-portal"),
    ]
)
