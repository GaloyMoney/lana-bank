compose_files = ["../docker-compose.yml", "../docker-compose.docker.yml"]
if os.path.exists("../docker-compose.override.yml"):
    compose_files.append("../docker-compose.override.yml")
if os.getenv("DAGSTER", "false") == "true":
    compose_files.append("../docker-compose.dagster.yml")
    dagster_override = "../.devenv/docker-compose.dagster.override.yml"
    if os.path.exists(dagster_override):
        compose_files.append(dagster_override)
if os.getenv("GOTENBERG", "false") == "true":
    compose_files.append("../docker-compose.gotenberg.yml")
    gotenberg_override = "../.devenv/docker-compose.gotenberg.override.yml"
    if os.path.exists(gotenberg_override):
        compose_files.append(gotenberg_override)

project_name = os.getenv("COMPOSE_PROJECT_NAME", "lana-bank")
docker_compose(compose_files, project_name = project_name)

docker_groups = {
    "auth": ["keycloak", "keycloak-pg", "oathkeeper"],
    "otel": ["otel-agent"],
    "core": ["core-pg"],
}

for service, deps in docker_groups.items():
    for dep in deps:
        dc_resource(dep, labels = [service])

if os.getenv("DAGSTER", "false") == "true":
    dagster_services = [
        {
            "service_name": "dagster_webserver",
            "link": link("http://localhost:" + os.getenv("DAGSTER_PORT", "3000"), "dagster-webserver")
        },
        {
            "service_name": "dagster_daemon"
        },
        {
            "service_name": "dagster_postgres"
        },
        {
            "service_name": "dagster-code-location-lana-dw"
        },
    ]
    for dagster_service in dagster_services:
        if dagster_service.get("link", None):
            dc_resource(
                dagster_service["service_name"],
                labels = ["dagster"],
                links = [
                    dagster_service.get("link", None)
                ]
            )
        else:
            dc_resource(
                dagster_service["service_name"],
                labels = ["dagster"]
            )

if os.getenv("GOTENBERG", "false") == "true":
    dc_resource("gotenberg", labels = ["gotenberg"])

serve_env_core = {
    "PG_CON": "postgres://user:password@localhost:" + os.getenv("CORE_PG_PORT", "5433") + "/pg?sslmode=disable",
    "OTEL_EXPORTER_OTLP_ENDPOINT": "http://localhost:" + os.getenv("OTEL_GRPC_PORT", "4317"),
}
local_resource(
    "core",
    labels = ["core"],
    serve_cmd = "cd .. && make setup-db run-server",
    serve_env = serve_env_core,
    readiness_probe = probe(
        initial_delay_secs = 3,
        period_secs = 1,
        http_get = http_get_action(
            path = "/graphql",
            port = int(os.getenv("ADMIN_SERVER_PORT", "5253")),
        ),
    ),
    allow_parallel = True,
    resource_deps = [
        "core-pg",
        "keycloak"
    ],
    links = [
        link("http://admin.localhost:" + os.getenv("OATHKEEPER_PROXY_PORT", "4455") + "/graphql", "playground"),
    ]
)

local_resource(
    "admin-panel",
    labels = ["apps"],
    serve_env = {
        "NEXT_PUBLIC_CORE_ADMIN_URL": "/graphql"
    },
    serve_cmd = "cd .. && make start-admin",
    readiness_probe = probe(
        initial_delay_secs = 3,
        period_secs = 1,
        http_get = http_get_action(
            path = "/",
            port = int(os.getenv("ADMIN_PANEL_PORT", "3001")),
        ),
    ),
    allow_parallel = True,
    links = [
        link("http://admin.localhost:" + os.getenv("OATHKEEPER_PROXY_PORT", "4455"), "admin-panel"),
    ]
)

local_resource(
    "customer-portal",
    labels = ["apps"],
    serve_env = {
        "NEXT_PUBLIC_CORE_APP_URL": "/graphql"
    },
    serve_cmd = "cd .. && make start-customer-portal",
    readiness_probe = probe(
        initial_delay_secs = 3,
        period_secs = 1,
        http_get = http_get_action(
            path = "/",
            port = int(os.getenv("CUSTOMER_PORTAL_PORT", "3002")),
        ),
    ),
    allow_parallel = True,
    links = [
        link("http://app.localhost:" + os.getenv("OATHKEEPER_PROXY_PORT", "4455"), "customer-portal"),
    ]
)
